        IDENTIFICATION DIVISION.
        PROGRAM-ID. EM04EX03.
        AUTHOR. VINICIUS ALVES.
        INSTALLATION. FATEC-SP.
        DATE-WRITTEN. 24/11/2020.
        DATE-COMPILED.
        SECURITY. ONLY AUTHOR MAY MODIFY.
      * REMARKS.

        ENVIRONMENT DIVISION.
        CONFIGURATION SECTION.
            SOURCE-COMPUTER. IBM-PC.
            OBJECT-COMPUTER. IBM-PC.
            SPECIAL-NAMES. DECIMAL-POINT IS COMMA.

        INPUT-OUTPUT SECTION.
        FILE-CONTROL.
            SELECT CADMERC ASSIGN TO DISK
            ORGANIZATION IS LINE SEQUENTIAL.
            SELECT MOVMERC ASSIGN TO DISK
            ORGANIZATION IS LINE SEQUENTIAL.
            SELECT ATUMERC ASSIGN TO DISK
            ORGANIZATION IS LINE SEQUENTIAL.
            SELECT RELERRO ASSIGN TO DISK.
            SELECT RELMERC ASSIGN TO DISK.

        DATA DIVISION.
        FILE SECTION.
        FD CADMERC
           LABEL RECORD IS STANDARD
           VALUE OF FILE-ID IS "CADMERC.DAT".
        01 REGMERC.
           02 COD-ANT       PIC 9(03).
           02 DESC-ANT      PIC X(30).
           02 ESTOQ-MIN-ANT PIC 9(03).
           02 ESTOQ-ANT     PIC 9(03).
           02 PRECO-ANT     PIC 9(04)V9(02).

        FD MOVMERC
           LABEL RECORD IS STANDARD
           VALUE OF FILE-ID IS "MOVMERC.DAT".
        01 REGMOV.
           02 COD-MOV       PIC 9(03).
           02 DESC-MOV      PIC X(30).
           02 ESTOQ-MIN-MOV PIC 9(03).
           02 ESTOQ-MOV     PIC 9(03).
           02 PRECO-MOV     PIC 9(04)V9(02).
           02 TIPO-MOV      PIC X.

        FD ATUMERC
           LABEL RECORD IS STANDARD
           VALUE OF FILE-ID IS "ATUMERC.DAT".
        01 REGATU.
           02 COD-ATU       PIC 9(03).
           02 DESC-ATU      PIC X(30).
           02 ESTOQ-MIN-ATU PIC 9(03).
           02 ESTOQ-ATU     PIC 9(03).
           02 PRECO-ATU     PIC 9(04)V9(02).

        FD RELERRO
           LABEL RECORD IS OMITTED.
        01 RELERR PIC X(80).

        FD RELMERC
           LABEL RECORD IS OMITTED.
        01 RELMC PIC X(80).

        WORKING-STORAGE SECTION.
        77 CH-MOV PIC X(03) VALUE SPACES.
        77 CH-ANT PIC X(03) VALUE SPACES.

        77 CT-LIN-ERR  PIC 99 VALUE 40.
        77 CT-LIN-MERC PIC 99 VALUE 40.

        77 CT-PAG-ERR  PIC 999 VALUE ZEROES.
        77 CT-PAG-MERC PIC 999 VALUE ZEROES.

        01 CAB-01-ERR.
           02 FILLER PIC X(29) VALUE SPACES.
           02 FILLER PIC X(22) VALUE "RELACAO DE MERCADORIAS".
           02 FILLER PIC X(21) VALUE SPACES.
           02 FILLER PIC X(05) VALUE "PAG. ".
           02 PAG-P-ERR PIC ZZ9.

        01 CAB-02-ERR.
           02 FILLER PIC X(30) VALUE SPACES.
           02 FILLER PIC X(20) VALUE "ERROS DE ATUALIZACAO".
           02 FILLER PIC X(30) VALUE SPACES.

        01 CAB-03-ERR.
           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(06) VALUE "CODIGO".
           02 FILLER PIC XX    VALUE SPACES.

           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(09) VALUE "DESCRICAO".
           02 FILLER PIC X(21) VALUE SPACES.
           02 FILLER PIC XX    VALUE SPACES.

           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(08) VALUE "MENSAGEM".
           02 FILLER PIC XX    VALUE SPACES.

        01 DETALHE-ERR.
           02 FILLER      PIC XX     VALUE SPACES.
           02 COD-REL-ERR PIC 9(03).
           02 FILLER      PIC XXX    VALUE SPACES.
           02 FILLER      PIC XX     VALUE SPACES.

           02 FILLER       PIC XX    VALUE SPACES.
           02 DESC-REL-ERR PIC X(30) VALUE SPACES.
           02 FILLER       PIC XX    VALUE SPACES.

           02 FILLER       PIC XX    VALUE SPACES.
           02 MSG-REL-ERR  PIC X(30) VALUE SPACES.
           02 FILLER       PIC XX    VALUE SPACES.

        01 CAB-01-MERC.
           02 FILLER PIC X(29) VALUE SPACES.
           02 FILLER PIC X(22) VALUE "RELACAO DE MERCADORIAS".
           02 FILLER PIC X(21) VALUE SPACES.
           02 FILLER PIC X(05) VALUE "PAG. ".
           02 PAG-P-MERC PIC ZZ9.

        01 CAB-02-MERC.
           02 FILLER PIC X(32) VALUE SPACES.
           02 FILLER PIC X(15) VALUE "ESTOQUE CRITICO".
           02 FILLER PIC X(33) VALUE SPACES.

        01 CAB-03-MERC.
           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(06) VALUE "CODIGO".
           02 FILLER PIC XX    VALUE SPACES.

           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(09) VALUE "DESCRICAO".
           02 FILLER PIC X(21) VALUE SPACES.
           02 FILLER PIC XX    VALUE SPACES.

           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(07) VALUE "ESTOQUE". *> MINIMO
           02 FILLER PIC XX    VALUE SPACES.

           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(10) VALUE "QUANTIDADE". *> EM ESTOQUE
           02 FILLER PIC XX    VALUE SPACES.

           02 FILLER PIC XXX   VALUE SPACES.
           02 FILLER PIC X(05) VALUE "PRECO".
           02 FILLER PIC XX    VALUE SPACES.

        01 CAB-04-MERC.
           02 FILLER PIC X(10) VALUE SPACES. *> CODIGO
           02 FILLER PIC X(13) VALUE SPACES. *> DESCRICAO

           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(06) VALUE "MINIMO".
           02 FILLER PIC XXX   VALUE SPACES.

           02 FILLER PIC XX    VALUE SPACES.
           02 FILLER PIC X(08) VALUE "UNITARIO".
           02 FILLER PIC XX    VALUE SPACES.

        01 DETALHE-MERC.
           02 FILLER              PIC XX VALUE SPACES.
           02 COD-MERC-REL        PIC 9(03).
           02 FILLER              PIC X(03) VALUE SPACES.
           02 FILLER              PIC XX VALUE SPACES.

           02 FILLER              PIC XX VALUE SPACES.
           02 DESC-MERC-REL       PIC X(30).
           02 FILLER              PIC XX VALUE SPACES.

           02 FILLER              PIC XX VALUE SPACES.
           02 ESTOQ-MIN-MERC-REL  PIC 9(03).
           02 FILLER              PIC XX VALUE SPACES.

           02 FILLER              PIC XX VALUE SPACES.
           02 ESTOQ-QTDE-MERC-REL PIC 9(03).
           02 FILLER              PIC XX VALUE SPACES.

           02 FILLER              PIC XX VALUE SPACES.
           02 PRECO-MERC-REL      PIC Z.Z99,99(03).
           02 FILLER              PIC XX VALUE SPACES.

        01 LIN-SPACES.
           02 FILLER PIC X(80) VALUE SPACES.

        PROCEDURE DIVISION.
        PGM.
            PERFORM INICIO.
            PERFORM PRINCIPAL UNTIL
                   CH-MOV EQUAL CH-ANT
               AND CH-ANT EQUAL HIGH-VALUES.
            PERFORM TERMINO.
            STOP RUN.

        INICIO.
            OPEN INPUT CADMERC MOVMERC
                 OUTPUT ATUMERC RELERRO RELMERC.
            PERFORM LER-MOV.
            PERFORM LER-MERC.

        LER-MOV.
            READ MOVMERC AT END MOVE HIGH-VALUES TO CH-MOV.
            IF CH-MOV IS NOT EQUAL TO HIGH-VALUES THEN
                MOVE COD-MOV TO CH-MOV.

        LER-MERC.
            READ CADMERC AT END MOVE HIGH-VALUES TO CH-ANT.
            IF CH-ANT IS NOT EQUAL TO HIGH-VALUES THEN
                MOVE COD-ANT TO CH-ANT.

        COPIA-RELERR.
            MOVE COD-MOV  TO COD-REL-ERR.
            MOVE DESC-MOV TO DESC-REL-ERR.

        COPIAATUTORELMERC.
            MOVE COD-ATU       TO COD-MERC-REL.
            MOVE DESC-ATU      TO DESC-MERC-REL.
            MOVE ESTOQ-MIN-ATU TO ESTOQ-MIN-MERC-REL.
            MOVE ESTOQ-ATU     TO ESTOQ-QTDE-MERC-REL.
            MOVE PRECO-ATU     TO PRECO-MERC-REL.

        PRINCIPAL.
            IF CH-MOV IS EQUAL TO CH-ANT
                PERFORM IGUAL
                PERFORM LER-MOV
                PERFORM LER-MERC
            ELSE
            IF CH-MOV IS LESS THAN CH-ANT
                PERFORM MOV-MENOR
                PERFORM LER-MOV
            ELSE
                PERFORM ANT-MENOR
                PERFORM LER-MERC.

        IGUAL.
            IF TIPO-MOV EQUAL "I"
                MOVE "INCLUSAO DE REG. JA EXISTENTE" TO MSG-REL-ERR
                PERFORM COPIA-RELERR
                PERFORM IMPRESSAO-ERR
                PERFORM GRAVA-ANT
            ELSE
            IF TIPO-MOV EQUAL "E"
                NEXT SENTENCE 
            ELSE
            IF TIPO-MOV EQUAL "A"
                PERFORM GRAVA-MOV.

        MOV-MENOR.
            IF TIPO-MOV EQUAL "I"
                PERFORM GRAVA-MOV
            ELSE
            IF TIPO-MOV EQUAL "E"
                MOVE "EXCLUSAO DE REG. NAO EXISTENTE" TO MSG-REL-ERR
                PERFORM COPIA-RELERR
                PERFORM IMPRESSAO-ERR
            ELSE
            IF TIPO-MOV EQUAL "A"
                MOVE "ALTERACAO DE REG. NAO EXISTENTE" TO MSG-REL-ERR
                PERFORM COPIA-RELERR
                PERFORM IMPRESSAO-ERR.

        ANT-MENOR.
            PERFORM GRAVA-ANT.

        GRAVA-ANT.
            MOVE COD-ANT       TO COD-ATU.
            MOVE DESC-ANT      TO DESC-ATU.
            MOVE ESTOQ-MIN-ANT TO ESTOQ-MIN-ATU.
            MOVE ESTOQ-ANT     TO ESTOQ-ATU.
            MOVE PRECO-ANT     TO PRECO-ATU.
            WRITE REGATU.

            IF ESTOQ-ATU NOT GREATER THAN ESTOQ-MIN-ATU THEN
               PERFORM COPIAATUTORELMERC
               PERFORM IMPRESSAO-MERC.

        GRAVA-MOV.
            MOVE COD-MOV       TO COD-ATU.
            MOVE DESC-MOV      TO DESC-ATU.
            MOVE ESTOQ-MIN-MOV TO ESTOQ-MIN-ATU.
            MOVE ESTOQ-MOV     TO ESTOQ-ATU.
            MOVE PRECO-MOV     TO PRECO-ATU.
            WRITE REGATU.

            IF ESTOQ-ATU NOT GREATER THAN ESTOQ-MIN-ATU THEN
               PERFORM COPIAATUTORELMERC
               PERFORM IMPRESSAO-MERC.

        IMPRESSAO-ERR.
            IF CT-LIN-ERR GREATER THAN 39 THEN PERFORM CABECALHO-ERR.
            PERFORM IMPDETALHE-ERR.

        IMPDETALHE-ERR.
            ADD 1 TO CT-LIN-ERR.
            WRITE RELERR FROM DETALHE-ERR AFTER ADVANCING 1 LINE.

        CABECALHO-ERR.
            MOVE ZEROES TO CT-LIN-ERR.
            ADD 1 TO CT-PAG-ERR.
            MOVE CT-PAG-ERR TO PAG-P-ERR.

            WRITE RELERR AFTER ADVANCING PAGE.
            WRITE RELERR FROM CAB-01-ERR AFTER ADVANCING 1 LINE.
            WRITE RELERR FROM CAB-02-ERR AFTER ADVANCING 2 LINES.
            WRITE RELERR FROM CAB-03-ERR AFTER ADVANCING 2 LINES.
            WRITE RELERR FROM LIN-SPACES AFTER ADVANCING 1 LINE.

        IMPRESSAO-MERC.
            IF CT-LIN-MERC GREATER THAN 39 THEN PERFORM CABECALHO-MERC.
            PERFORM IMPDETALHE-MERC.

        IMPDETALHE-MERC.
            ADD 1 TO CT-LIN-MERC.
            WRITE RELMC FROM DETALHE-MERC AFTER ADVANCING 1 LINE.

        CABECALHO-MERC.
            MOVE ZEROES TO CT-LIN-MERC.
            ADD 1 TO CT-PAG-MERC.
            MOVE CT-PAG-MERC TO PAG-P-MERC.

            WRITE RELMC AFTER ADVANCING PAGE.
            WRITE RELMC FROM CAB-01-MERC AFTER ADVANCING 1 LINE.
            WRITE RELMC FROM CAB-02-MERC AFTER ADVANCING 2 LINES.
            WRITE RELMC FROM CAB-03-MERC AFTER ADVANCING 2 LINES.
            WRITE RELMC FROM CAB-04-MERC AFTER ADVANCING 1 LINE.
            WRITE RELMC FROM LIN-SPACES  AFTER ADVANCING 1 LINE.

        TERMINO.
            CLOSE CADMERC MOVMERC ATUMERC.